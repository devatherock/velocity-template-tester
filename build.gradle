plugins {
    id "org.jetbrains.kotlin.jvm" version "${kotlinVersion}"
    id "org.jetbrains.kotlin.kapt" version "${kotlinVersion}"
    id "org.jetbrains.kotlin.plugin.allopen" version "${kotlinVersion}"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id 'io.micronaut.application' version '4.4.5'
    id 'groovy'
    id 'jacoco'
    id 'org.sonarqube' version '6.0.1.5171'
    id 'com.diffplug.spotless' version '7.0.2'
}

version "2.2.0"
group "io.github.devatherock"

repositories {
    mavenCentral()
}

configurations {
    // for dependencies that are needed for development only
    developmentOnly

    // Force dependency versions to use dependencies without vulnerabilities
    all {
        resolutionStrategy {
            force 'org.jsoup:jsoup:1.18.3'
        }
    }
}

dependencies {
    kapt "io.micronaut.openapi:micronaut-openapi"
    kapt "io.micronaut.validation:micronaut-validation-processor"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"

    implementation "io.micronaut.validation:micronaut-validation"
    implementation "io.micronaut:micronaut-runtime"
    implementation "io.swagger.core.v3:swagger-annotations"
    implementation "io.micronaut:micronaut-management"
    implementation "io.micronaut.micrometer:micronaut-micrometer-core"
    implementation "io.micronaut:micronaut-jackson-databind"
    implementation "io.micronaut:micronaut-http-server-netty"

    implementation group: 'org.apache.velocity', name: 'velocity-engine-core', version: '2.4.1'
    implementation group: 'org.apache.velocity.tools', name: 'velocity-tools-generic', version: '3.1'
    implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '8.0'
    implementation group: 'javax.inject', name: 'javax.inject', version: '1'
    implementation group: 'org.yaml', name: 'snakeyaml'

    runtimeOnly "com.fasterxml.jackson.module:jackson-module-kotlin:2.18.2"
    runtimeOnly "ch.qos.logback:logback-classic:1.5.17"

    testImplementation "io.micronaut:micronaut-inject-groovy"
    testImplementation "io.micronaut.test:micronaut-test-spock"
    testImplementation "io.micronaut:micronaut-http-client"

    testImplementation group: 'org.apache.groovy', name: 'groovy-json', version: '4.0.25'
    testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.3-groovy-4.0'
}

test.classpath += configurations.developmentOnly

mainClassName = "io.github.devatherock.velocity.tester.Application"

tasks.withType(Test) {
    useJUnitPlatform()
    environment('JAVA_OPTS', System.getenv('JAVA_OPTS') ?: '-Xmx640m')
}

allOpen {
    annotation("io.micronaut.aop.Around")
}

compileKotlin {
    kotlinOptions {
        // Will retain parameter names for Java reflection
        javaParameters = true
    }
}

shadowJar {
    mergeServiceFiles()
}

run {
    classpath += configurations.developmentOnly
    jvmArgs('-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote', '-XshowSettings:vm')
    systemProperties(System.properties)
}

ext.jacoco = [
    exclusions: [
        'io/github/devatherock/velocity/tester/Application.class',
        'io/github/devatherock/velocity/tester/util/VelocityUtil.class'
    ],
    coverageThresholds: [
        'io.github.devatherock.velocity.tester.controller.VelocityController': [
            'BRANCH': 0.50,
            'COMPLEXITY': 0.50,
            'INSTRUCTION': 0.81
        ],
        'io.github.devatherock.velocity.tester.util.VelocityUtil.Companion': [
            'BRANCH': 0.90,
            'COMPLEXITY': 0.83,
            'INSTRUCTION': 0.95
        ],
        'io.github.devatherock.velocity.tester.model.ExpandTemplateRequest': [
            'BRANCH': 0.50,
            'COMPLEXITY': 0.80,
            'INSTRUCTION': 0.78
        ]
    ]
]

apply from: "${getPropertyValue('gradle.includes.location', 'https://raw.githubusercontent.com/devatherock/gradle-includes/master')}/integration.gradle"
apply from: "${getPropertyValue('gradle.includes.location', 'https://raw.githubusercontent.com/devatherock/gradle-includes/master')}/checks.gradle"

/**
 * Fetches the value of a gradle property
 *
 * @param propertyName
 * @param propertyValueDefault
 * @return the property value
 */
String getPropertyValue(String propertyName, String propertyValueDefault = null) {
    return project.hasProperty(propertyName) ? project[propertyName] : propertyValueDefault
}
